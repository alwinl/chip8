<program>         ::= <decl>* ;

<decl>            ::= <func_decl> | <var_decl> ;

<func_decl>       ::= KEYWORD("func") IDENTIFIER PUNCTUATION("(") <var_decl>* PUNCTUATION(")")
						PUNCTUATION("{")
							<var_decl>*
							<stmt>*
						PUNCTUATION("}") ;

<var_decl>        ::= KEYWORD("var") (IDENTIFIER PUNCTUATION(":") TYPE_KEYWORD) PUNCTUATION(";") ;

<stmt>            ::= <expr_stmt>
                   | <if_stmt>
                   | <while_stmt>
                   | <draw_stmt>
                   | <return_stmt>
                   | <block_stmt>
				   | <assignment>
                   ;

<expr_stmt>       ::= <expr> PUNCTUATION(";") ;

<if_stmt>         ::= KEYWORD("if") PUNCTUATION("(") <expr> PUNCTUATION(")") <stmt> ( KEYWORD("else") <stmt> )? ;

<while_stmt>      ::= KEYWORD("while") PUNCTUATION("(") <expr> PUNCTUATION(")") <stmt>  ;

<draw_stmt>       ::= KEYWORD("draw") IDENTIFIER PUNCTUATION(",") <expr> PUNCTUATION(",") <expr> ( PUNCTUATION(",") <expr> )? PUNCTUATION(";") ; // draw sprite, x, y [, height]

<return_stmt>     ::= KEYWORD("return") <expr>? PUNCTUATION(";") ;

<block_stmt>      ::= PUNCTUATION("{") <stmt>* PUNCTUATION("}") ;

<assignment>      ::= IDENTIFIER OPERATOR("=") <expr> ;


<expr>            ::= <logic_or> ;

<logic_or>        ::= <logic_and> ( OPERATOR("||") <logic_and> )* ;

<logic_and>       ::= <equality> ( OPERATOR("&&") <equality> )* ;

<equality>        ::= <comparison> ( (OPERATOR("==") | OPERATOR("!=") ) <comparison> )* ;

<comparison>      ::= <term> ( (OPERATOR(">") | OPERATOR(">=")  | OPERATOR("<") | OPERATOR("<=")) <term>)* ;

<term>            ::= <factor> ( (OPERATOR("+") | OPERATOR("-")) <factor>)* ;

<factor>          ::= <unary> ( (OPERATOR("*") | OPERATOR("/")) <unary>)* ;

<unary>           ::= (OPERATOR("!") | OPERATOR("-")) <unary> | <primary> ;

<primary>         ::= NUMBER
                   | KEYWORD("true")
                   | KEYWORD("false")
                   | KEYWORD("bcd")
                   | KEYWORD("rnd")
                   | KEYWORD("keydown")
                   | KEYWORD("getkey")
                   | PUNCTUATION("(") <expr> PUNCTUATION(")")
                   | IDENTIFIER ( PUNCTUATION("(") (<expr> (PUNCTUATION(",") <expr>)*)? PUNCTUATION(")") )?
                   ;
