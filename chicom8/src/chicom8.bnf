<program>         ::= <decl>* ;

<decl>            ::= <func_decl> | <var_decl> ;

<func_decl>       ::= <func> <identifier> <open_bracket> <param_list>? <close_bracket> <open_curly> <func_body> <close_curly> ;

<func_body>       ::= <func_imp>*  ;

<func_imp>        ::= <var_decl> | <stmt> ;

<param_list>      ::= <typed_identifier> ( <comma> <typed_identifier> )* ;

<var_decl>        ::= <var> <typed_identifier> <semi_colon> ;

<typed_identifier> ::= <identifier> <colon> <type> ;

<type> ::= <nibble_t>
         | <byte_t>
         | <snack_t>
         | <number_t>
         | <bool_t>
         | <key_t>
         | <sprite_t>
         ;

<stmt>            ::= <expr_stmt>
                   | <if_stmt>
                   | <while_stmt>
                   | <draw_stmt>
                   | <return_stmt>
                   | <block>
                   ;

<expr_stmt>       ::= <expr> <semi_colon> ;

<if_stmt>         ::= <if> <open_bracket> <expr> <close_bracket> <stmt> ( <else> <stmt> )? ;

<while_stmt>      ::= <while> <open_bracket> <expr> <close_bracket> <stmt>  ;

<draw_stmt>       ::= <draw> <identifier> <comma> <expr> <comma> <expr> ( <comma> <expr> )? <semi_colon> ; // draw sprite, x, y [, height]

<return_stmt>     ::= <return> <expr>? <semi_colon> ;

<block>           ::= <open_curly> <stmt>* <close_curly> ;


<expr>            ::= <identifier> <equal_operator> <expr> | <logic_or> ;

<logic_or>        ::= <logic_and> ( <or_operator> <logic_and> )* ;

<logic_and>       ::= <equality> ( <and_operator> <equality> )* ;

<equality>        ::= <comparison> ( (<equality_operator> | <inequality_operator> ) <comparison> )* ;

<comparison>      ::= <term> ( (<gt_than_operator> | <gteq_than_operator>  | <lt_than_operator> | <lteq_than_operator>) <term>)* ;

<term>            ::= <factor> ( (<plus_operator> | <minus_operator>) <factor>)* ;

<factor>          ::= <unary> ( (<mult_operator> | <div_operator>) <unary>)* ;

<unary>           ::= (<not_operator> | <minus_operator>) <unary> | <primary> ;

<primary>         ::= <number>
                   | <true>
                   | <false>
                   | <bcd>
                   | <rnd>
                   | <keydown>
                   | <getkey>
                   | <open_bracket> <expr> <close_bracket>
                   | <identifier> ( <open_bracket> (<expr> (<comma> <expr>)*)? <close_bracket> )?
                   ;



<true>    ::= KEYWORD("true") ;
<false>   ::= KEYWORD("false") ;
<bcd>     ::= KEYWORD("bcd") ;
<rnd>     ::= KEYWORD("rnd") ;
<keydown> ::= KEYWORD("keydown") ;
<getkey>  ::= KEYWORD("getkey") ;
<func>    ::= KEYWORD("func") ;
<var>     ::= KEYWORD("var") ;
<if>      ::= KEYWORD("if") ;
<else>    ::= KEYWORD("else") ;
<while>   ::= KEYWORD("while") ;
<draw>    ::= KEYWORD("draw") ;
<return>  ::= KEYWORD("return") ;

<nibble_t> ::= TYPE_KEYWORD("nibble") ;
<byte_t>   ::= TYPE_KEYWORD("byte") ;
<snack_t>  ::= TYPE_KEYWORD("snack") ;
<number_t> ::= TYPE_KEYWORD("number") ;
<bool_t>   ::= TYPE_KEYWORD("bool") ;
<key_t>    ::= TYPE_KEYWORD("key") ;
<sprite_t> ::= TYPE_KEYWORD("sprite") ;


<identifier> ::= IDENTIFIER ;
<number>     ::= NUMBER ;

<equality_operator>   ::= OPERATOR("==") ;
<inequality_operator> ::= OPERATOR("!=") ;
<gt_than_operator>    ::= OPERATOR(">") ;
<gteq_than_operator>  ::= OPERATOR(">=") ;
<lt_than_operator>    ::= OPERATOR("<") ;
<lteq_than_operator>  ::= OPERATOR("<=") ;
<plus_operator>       ::= OPERATOR("+") ;
<minus_operator>      ::= OPERATOR("-") ;
<mult_operator>       ::= OPERATOR("*") ;
<div_operator>        ::= OPERATOR("/") ;
<not_operator>        ::= OPERATOR("!") ;
<equal_operator>      ::= OPERATOR("=") ;
<or_operator>         ::= OPERATOR("||") ;
<and_operator>        ::= OPERATOR("&&") ;

<comma>          ::= PUNCTUATION(",") ;
<colon>          ::= PUNCTUATION(":");
<semi_colon>     ::= PUNCTUATION(";") ;
<open_bracket>   ::= PUNCTUATION("(") ;
<close_bracket>  ::= PUNCTUATION(")") ;
<open_curly>     ::= PUNCTUATION("{") ;
<close_curly>    ::= PUNCTUATION("}") ;
