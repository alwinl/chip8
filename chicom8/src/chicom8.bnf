<program>         ::= <decl>* ;

<decl>            ::= <func_decl> | <var_decl> ;

<func_decl>       ::= KEYWORD("func") IDENTIFIER PUNCTUATION("(") <param_list>? PUNCTUATION(")") PUNCTUATION("{") <func_body> PUNCTUATION("}") ;

<func_body>       ::= <func_imp>*  ;

<func_imp>        ::= <var_decl> | <stmt> ;

<param_list>      ::= <typed_identifier> ( PUNCTUATION(",") <typed_identifier> )* ;

<var_decl>        ::= KEYWORD("var") <typed_identifier> PUNCTUATION(";") ;

<typed_identifier> ::= IDENTIFIER PUNCTUATION(":") <type> ;

<type> ::= TYPE_KEYWORD("nibble")
         | TYPE_KEYWORD("byte")
         | TYPE_KEYWORD("snack")
         | TYPE_KEYWORD("number")
         | TYPE_KEYWORD("bool")
         | TYPE_KEYWORD("key")
         | TYPE_KEYWORD("sprite") <expr>
         ;

<stmt>            ::= <expr_stmt>
                   | <if_stmt>
                   | <while_stmt>
                   | <draw_stmt>
                   | <return_stmt>
                   | <block>
                   ;

<expr_stmt>       ::= <expr> PUNCTUATION(";") ;

<if_stmt>         ::= KEYWORD("if") PUNCTUATION("(") <expr> PUNCTUATION(")") <stmt> ( KEYWORD("else") <stmt> )? ;

<while_stmt>      ::= KEYWORD("while") PUNCTUATION("(") <expr> PUNCTUATION(")") <stmt>  ;

<draw_stmt>       ::= KEYWORD("draw") <identifier> PUNCTUATION(",") <expr> PUNCTUATION(",") <expr> ( PUNCTUATION(",") <expr> )? PUNCTUATION(";") ; // draw sprite, x, y [, height]

<return_stmt>     ::= KEYWORD("return") <expr>? PUNCTUATION(";") ;

<block>           ::= PUNCTUATION("{") <stmt>* PUNCTUATION("}") ;


<expr>            ::= <identifier> OPERATOR("=") <expr> | <logic_or> ;

<logic_or>        ::= <logic_and> ( OPERATOR("||") <logic_and> )* ;

<logic_and>       ::= <equality> ( OPERATOR("&&") <equality> )* ;

<equality>        ::= <comparison> ((OPERATOR("==") | OPERATOR("!=") ) <comparison>)* ;

<comparison>      ::= <term> ((OPERATOR(">") | OPERATOR(">=") | OPERATOR("<") | OPERATOR("<=") ) <term>)* ;

<term>            ::= <factor> ((OPERATOR("+") | OPERATOR("-") ) <factor>)* ;

<factor>          ::= <unary> ( (OPERATOR("*") | OPERATOR("/") ) <unary>)* ;

<unary>           ::= ( OPERATOR("!") | OPERATOR("-") ) <unary> | <primary> ;

<primary>         ::= <number>
                   | KEYWORD("true")
                   | KEYWORD("false")
                   | KEYWORD("bcd") <identifier>
                   | KEYWORD("rnd") <expr>
                   | KEYWORD("keydown") <identifier>
                   | KEYWORD("getkey")
                   | PUNCTUATION("(") <expr> PUNCTUATION(")")
                   | <identifier> ( PUNCTUATION("(") (<expr> (PUNCTUATION(",") <expr>)*)? PUNCTUATION(")") )?
                   ;

<identifier>      ::= IDENTIFIER ;
<number>          ::= NUMBER ;